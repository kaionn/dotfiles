# ============================================
# PATH と環境変数
# ============================================
export HOMEBREW_PREFIX="/usr/local"
export HOMEBREW_CELLAR="/usr/local/Cellar"
export HOMEBREW_REPOSITORY="/usr/local/Homebrew"
eval $(/usr/local/bin/brew shellenv)
typeset -U PATH path
export VOLTA_HOME="$HOME/.volta"
path=("$VOLTA_HOME/bin" "$HOME/.local/bin" "$HOME/.lmstudio/bin" "$HOME/.antigravity/antigravity/bin" $path)
export PATH

# asdf version manager
. $(brew --prefix asdf)/libexec/asdf.sh

export LANG=en_US.UTF-8
export HISTSIZE=10000
export SAVEHIST=10000
export MANPAGER="col -b -x|vim -R -c 'set ft=man nolist nomod noma' -"

# ============================================
# Zsh オプション
# ============================================
autoload -U +X compinit && compinit

setopt auto_cd
setopt hist_ignore_dups
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt share_history
setopt complete_in_word

# ============================================
# 補完設定
# ============================================
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' menu select

# ============================================
# エイリアス
# ============================================
if command -v eza &> /dev/null; then
    alias ls='eza --group-directories-first'
    alias tree='eza --tree'
else
    alias ls='ls -G'
fi

alias note='vim -c "setlocal buftype=nofile bufhidden=wipe noswapfile filetype=markdown" /tmp/note.md'
alias dl='yt-dlp -o "%(title)s.%(ext)s"'

# ============================================
# 関数
# ============================================
function peco-select-history() {
    BUFFER=$(fc -l -n 1 | tail -r | awk '!a[$0]++' | peco --query "$LBUFFER")
    CURSOR=${#BUFFER}
}

zle -N peco-select-history
bindkey '^R' peco-select-history

# Git Project Selector
function cd-git-project() {
  local search_dir="$HOME/dev/src/github.com"
  local selected_dir=""
  # 高速化: fdがインストールされている場合はfdを使用
  if command -v fd > /dev/null; then
    # -H: 隠しファイル(.git)を検索
    # -t d: ディレクトリのみ
    # --max-depth 3: 探索深度を制限 (github.com/user/repo/.git なので 3)
    # -x dirname: 見つかったパスの親ディレクトリを表示
    selected_dir=$(fd -H -t d '^.git$' "$search_dir" --max-depth 3 -x dirname {} | \
      fzf --height 40% --layout=reverse --border --prompt="Project > ")
  else
    # findを使用する場合も maxdepth で探索範囲を制限して高速化
    selected_dir=$(find "$search_dir" -maxdepth 3 -type d -name ".git" -prune -exec dirname {} \; | \
      fzf --height 40% --layout=reverse --border --prompt="Project > ")
  fi
  if [ -n "$selected_dir" ]; then
    BUFFER="cd ${(q)selected_dir}"
    zle accept-line
  fi
  zle reset-prompt
}
# Register the widget
zle -N cd-git-project
# Keybinding
# 注意: "Command + ]" を直接zshでバインドすることはできません。
# 以下のいずれかの方法で設定してください。
# 方法1: Control + ] を使用する場合 (推奨・設定不要)
bindkey '^]' cd-git-project

# Git Branch Selection Widget
function select-git-branch() {
  local branch
  # List branches, exclude HEAD, use fzf for selection
  branch=$(git branch --all | grep -v HEAD | fzf --height 40% --layout=reverse --border --prompt="Branch > " | sed "s/.* //" | sed "s#remotes/[^/]*/##")
  if [ -n "$branch" ]; then
    BUFFER="${BUFFER}${branch}"
    CURSOR=$#BUFFER
  fi
  zle reset-prompt
}
zle -N select-git-branch
# Bind to Ctrl+b
bindkey "^b" select-git-branch

# ============================================
# プロンプト & プラグイン
# ============================================
eval "$(starship init zsh)"

source $HOMEBREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh
source $HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# ============================================
# 起動時の設定
# ============================================
cd "$HOME/Library/Mobile Documents/com~apple~CloudDocs"

# direnv hook
eval "$(direnv hook zsh)"
